group 'com.qztech.stone_smart_flutter'
version '1.0'

buildscript {
    ext {
        kotlin_version = '1.9.0'
        stone_sdk_version = '4.10.2'

        localProp = new Properties()
        fileName = 'local.properties'
        if (project.rootProject.file(fileName).exists()) {
            localProp.load(new FileInputStream(rootProject.file(fileName)))
        }
        packageCloudReadToken = System.env.PACKAGECLOUD_READ_TOKEN != null ? System.env.PACKAGECLOUD_READ_TOKEN  : localProp["packageCloudReadToken"] ?: ""
        packageCloudReadTokenInternal = System.env.PACKAGECLOUD_READ_TOKEN_INTERNAL ?: localProp["packageCloudReadTokenInternal"]
        println "packageCloudReadToken: $packageCloudReadToken"
        println "packageCloudReadTokenInternal: $packageCloudReadTokenInternal"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://packagecloud.io/priv/${packageCloudReadToken}/stone/pos-android/maven2" }
        maven { url "https://packagecloud.io/priv/${packageCloudReadTokenInternal}/stone/pos-android-internal/maven2" }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace = "com.qztech.stone_smart_flutter"

    compileSdk = 35

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/api_release.kotlin_module'
        exclude 'META-INF/client_release.kotlin_module'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
       minSdk = 22
    }
}

dependencies{
    implementation "br.com.stone:stone-sdk:$stone_sdk_version"
    implementation "br.com.stone:stone-sdk-posandroid:$stone_sdk_version"
    implementation "br.com.stone:stone-sdk-posandroid-positivo:$stone_sdk_version"

    implementation "br.com.stone:stone-sdk-posandroid-ingenico:$stone_sdk_version"
    implementation "br.com.stone:stone-sdk-posandroid-sunmi:$stone_sdk_version"
    implementation "br.com.stone:stone-sdk-posandroid-gertec:$stone_sdk_version"

    // debugImplementation "com.github.tony19:logback-android:2.0.0"
    // debugImplementation 'br.com.stone.sdk.android:debugmode:4.0.3'
}
